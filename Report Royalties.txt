SET QUOTED_IDENTIFIER ON 
GO
SET ANSI_NULLS ON 
GO

ALTER          PROCEDURE dbo.psp_ReportRoyalties
@Month char(2),
@Year char(4)
 AS

-- This procedure prepares data for reporting royalty reports
-- to software vendors and consultants to whom we owe
-- royalties for the privilege of processing data that
-- is generated by software which they support.
-- Output goes to Crystal report Royalties.rpt


DECLARE @BegDate smalldatetime
DECLARE @EndDate smalldatetime
SET @BegDate = @Month + '/01/' + @Year
SET @EndDate = DATEADD(dd,-1,(DATEADD(mm,1,@BegDate)))

DECLARE @Work1 TABLE
(ROY_ID int,
 ROY_Name varchar(50),
 ROY_RemitTo varchar(50),
 ROY_Addr1 varchar(50),
 ROY_Addr2 varchar(50),
 ROY_CityStateZip varchar(60),
 ROY_Country varchar(50),
 ROY_LetterPageFee smallmoney,
 ROY_NCOAFee smallmoney,
 ROY_ACSFee smallmoney,
 CSV_VendorName varchar(50),
 NCOA decimal(18,0),
 Letters decimal(18,0),
 ACS decimal(18,0),
 CL_CID varchar(10),
 CL_CustNo varchar(10),
 CL_Name varchar(50),
 CL_ID int,
 BegDate smalldatetime,
 EndDate smalldatetime)

-- Step 1: Create the base records
INSERT @Work1
(ROY_ID,
ROY_Name,
ROY_RemitTo,
ROY_Addr1,
ROY_Addr2,
ROY_CityStateZip,
ROY_Country,
ROY_LetterPageFee,
ROY_NCOAFee,
ROY_ACSFee,
CSV_VendorName,
NCOA,
Letters,
ACS,
CL_CID,
CL_CustNo,
CL_Name,
CL_ID,
BegDate,
EndDate)
SELECT DISTINCT
R.ROY_ID,
R.ROY_Name, 
R.ROY_RemitTo, 
R.ROY_Addr1, 
R.ROY_Addr2, 
((CASE WHEN (LEN(R.ROY_City) > 0) THEN R.ROY_City + ', ' ELSE '***No city ' END) +
(CASE WHEN (ST.ST_Code IS NULL) THEN ' ***No state ' ELSE ST.ST_Code + ' ' END) + 
(CASE WHEN (LEN(R.ROY_Zip) < 5) THEN ' ***Invalid zip' ELSE R.ROY_Zip END)),
R.ROY_Country, 
R.ROY_LetterPageFee, 
R.ROY_NCOAFee, 
R.ROY_ACSFee,
CSV.CSV_VendorName,
0,
0,
0,
CL.CL_CID,
CL.CL_CustomerNumber,
CL.CL_Name,
CL.CL_ID,
@BegDate,
@EndDate
FROM Client CL
JOIN ClientSoftwareVendor CSV
ON (CL.CSV_ID = CSV.CSV_ID)
JOIN Royalties R
ON (R.CSV_ID = CSV.CSV_ID AND CL.ROY_ID = R.ROY_ID)
LEFT OUTER JOIN State ST
ON (R.ST_ID = ST.ST_ID)


-- Step 2: Add NCOA counts from MOAT_Component
DECLARE @Work2 TABLE
(ROY_ID int,
 CL_ID int,
 QTY decimal(18,0))

INSERT @Work2
SELECT W1.ROY_ID,
	   W1.CL_ID,
       SUM(MC.MxC_Qty)
FROM @Work1 W1
JOIN [GANDALF\MOAD].MOAD.dbo.MOAT_Component MC
ON (W1.CL_ID = MC.CL_ID AND 
(MC.MxC_ProcessDate BETWEEN @BegDate AND @EndDate) AND
 MC.C_ID IN (1,318,340)) -- NCOA
GROUP BY W1.ROY_ID, W1.CL_ID

UPDATE @Work1
SET NCOA = W2.Qty
FROM @Work1 W1
JOIN @Work2 W2
ON (W1.ROY_ID = W2.ROY_ID AND
    W1.CL_ID = W2.CL_ID)

DELETE @Work2

-- Step 3: Add Letter counts from MOAT_Component
INSERT @Work2
SELECT W1.ROY_ID,
	   W1.CL_ID,
       SUM(MC.MxC_Qty)
FROM @Work1 W1
JOIN [GANDALF\MOAD].MOAD.dbo.MOAT_Component MC
ON (W1.CL_ID = MC.CL_ID AND 
(MC.MxC_ProcessDate BETWEEN @BegDate AND @EndDate) AND
 MC.MxC_Type = 'L') -- Letters
GROUP BY W1.ROY_ID, W1.CL_ID

UPDATE @Work1
SET Letters = W2.Qty
FROM @Work1 W1
JOIN @Work2 W2
ON (W1.ROY_ID = W2.ROY_ID AND
    W1.CL_ID = W2.CL_ID)

DELETE  @Work2

-- Step 4: Add ACS counts from MOAT_Component
INSERT @Work2
SELECT W1.ROY_ID,
   	   W1.CL_ID,
       SUM(MC.MxC_Qty)
FROM @Work1 W1
JOIN [GANDALF\MOAD].MOAD.dbo.MOAT_Component MC
ON (W1.CL_ID = MC.CL_ID AND 
(MC.MxC_ProcessDate BETWEEN @BegDate AND @EndDate) AND
 MC.C_ID IN (374,377)) -- ACS
GROUP BY W1.ROY_ID, W1.CL_ID

UPDATE @Work1
SET ACS = W2.Qty
FROM @Work1 W1
JOIN @Work2 W2
ON (W1.ROY_ID = W2.ROY_ID AND
    W1.CL_ID = W2.CL_ID)

DELETE @Work2

-- Step 4: Add ACS counts from OtherCharges
INSERT @Work2
SELECT W1.ROY_ID,
	   W1.CL_ID,
       SUM(OC.OC_Count)
FROM @Work1 W1
JOIN OtherCharge OC
ON (W1.CL_ID = OC.CL_ID AND 
(dbo.pudf_DateOnly(OC.OC_TransDate) BETWEEN @BegDate AND @EndDate) AND
 OC.C_ID = 7) -- ACS
GROUP BY W1.ROY_ID, W1.CL_ID

UPDATE @Work1
SET ACS = ACS + W2.Qty
FROM @Work1 W1
JOIN @Work2 W2
ON (W1.ROY_ID = W2.ROY_ID AND
    W1.CL_ID = W2.CL_ID)

--SELECT * FROM @Work1


-- Step 5: Set NCOA fee rates for DAKCS clients
UPDATE @Work1
SET ROY_NCOAFee = CLxC_Price * .1  -- rate is 10% of charge
FROM @Work1 W
JOIN Client_Component CC
ON (W.CL_ID = CC.CL_ID AND CC.C_ID = 318)  -- NCOA2
JOIN Client CL
ON (W.CL_ID = CL.CL_ID)
WHERE CL.ROY_ID = 17 -- DAKCS royalty

SELECT * FROM @Work1







GO

SET QUOTED_IDENTIFIER OFF 
GO
SET ANSI_NULLS ON 
GO

